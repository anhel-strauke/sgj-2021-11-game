label chapter_10:
    $ location("Район Пахифеи")

    define h10 = CharacterAt(hero, (1160 , 540), "left_tall")
    define h10_idea = CharacterAt(hero, (740, 140), "top_left_th")
    define r10 = CharacterAt(rogue, (1140 , 440), "right_tall")
    define s10 = CharacterAt(rogue, (1640 , 240), "right_tall")
    define a10 = CharacterAt(rogue, (1340 , 640), "right_tall")
    define p10 = CharacterAt(poor, (1240 , 440), "right_tall")
    define p10_long = CharacterAt(poor, (1335, 90), "top_right_big")
    define iso5_10 = CharacterAt(iso, (0.4, 0.3), "right_tall", name = "Голос из громкоговорителя")

    if Myo_value > 2:
        jump chapter_10s1

    scene black

    show bg industrial2:
        alpha 0
        zoom 0.5
        linear 2 alpha 1

    show gg_ride:
        zoom 0.3
        xpos 10 ypos 400

    h10_idea "Ох, ну и запах. Это все из-за отходов фабрики.."
    pause 1.0
    h10_idea "Район Пахифеи… Здешняя производственная фабрика Пахлстона разрослась и захватила часть городских доков Феилтон."
    h10_idea "Так этот район и получил свое название."
    pause 1.0
    h10_idea "По крайней мере фабрика дает людям работу."
    h10_idea "Ой, я должна проверить- на месте ли лекарства."
    pause 2.0

    show poor:
        xpos 1200 ypos 0.3
        zoom 0.3
        alpha 0
        linear 2 alpha 1

    p10 "Приветствую вас в Пахифеи. Самом свободном районе Ривенбурга."

    h10 "А! Вы меня напугали… Кто вы?"

    p10 "Я прощу прощения. Зовите меня Яноро."
    p10_long "Позвольте спросить, что вы делаете в таком месте как Пахифеи в столь поздний час? К тому же, с полной сумкой вакцины."

    h10 "Что? Откуда вы знаете, что у меня в сумке?"

    p10_long "Вы сами показали содержимое всей улице, когда открыли сумку. Вы даже не подозреваете, какие люди здесь живут"

    h10 "Эти лекарства… Это имущество директора фабрики. Я…"

    p10 "...Всего лишь курьер, который их доставляет. Я так и думал."
    p10_long "Послушайте меня, вы сейчас находитесь в районе, где живут работники этой фабрики со своими семьями, как и я сам."
    p10_long "Все мы страдаем от отравленного химикатами воздуха и единственное, что может нам помочь- это дорогое лекарство, которое вы несете в сумке."

    h10 "Как это меня касается? Я всего лишь курьер!"
    p10 "Вы больше, чем курьер."
    p10_long "Вы- мессия для жителей этого района. Мы бедны и больны. Нам нечего вам дать взамен, но только вы можете спасти нас."
    h10 "Как я могу это сделать?"
    h10 "У меня есть четкие инструкции донести посылку на фабрику."
    p10_long "Посылку с вакциной вы несете директору фабрики и его семье. У них достаточно средств, чтобы купить себе еще."
    p10_long "Все просто. Вы отдаете лекарство тем, кто действительно в них нуждается. Ну же, отдайте мне посылку"

    menu:
        "Хорошо. Возьмите посылку и передайте тем, кто болеет":
            p10_long "Говорю спасибо вам от всех жителей района Пахифеи. Вы спасли многих из нас"
            h10_idea "Правильно ли я поступила?"
            h10_idea "Наверное, не стоит говорить об этом событии госпоже Амелинде. А если спросит- технически, меня просто ограбили."
        "Не мешайте мне делать мою работу!":
            p10_long "Вы пытаетесь отсрочить неизбежное! За каждый свой поступок вы несете ответственность.."
            hide poor
            h10_idea "Теперь я могу спокойно отнести посылку. Оу, вот и фабрика."
            iso5_10 "Представьтесь!"
            h10 "Я курьер Мэллори из аптеки Амелинды"
            iso5_10 "Мы ждали вас."
            iso5_10 "Положите посылку в разъем у стены и заберите оттуда деньги за доставку."
            h10_idea "Интересно, у них все здесь автоматизировано?"
            iso5_10 "*Шуршание одежды*"
            h10_idea "Даже чаевые положили! Неплохой конец дня."
            $ money_value += 10

    jump chapter_11

label chapter_10s1:

    scene black

    show bg industrial2:
        zoom 0.5
    show gg_ride:
        zoom 0.3
        xpos 10 ypos 400
    with Dissolve(2)

    h10_idea "Ох, ну и запах. Это все из-за отходов фабрики.."
    pause 1.0
    h10_idea "Район Пахифеи… Здешняя производственная фабрика Пахлстона разрослась и захватила часть городских доков Феилтон."
    h10_idea "Так этот район и получил свое название."
    pause 1.0
    h10_idea "По крайней мере фабрика дает людям работу."
    h10_idea "Ой, я должна проверить- на месте ли лекарства."
    pause 2.0

    show rogue:
        xpos 1100 ypos 0.3
        zoom 0.3
        alpha 0
        linear 2 alpha 1

    show rogue2:
        xpos 1300 ypos 0.3
        zoom 0.3
        alpha 0
        linear 2 alpha 1

    show rogue3:
        xpos 1500 ypos 0.3
        zoom 0.3
        alpha 0
        linear 2 alpha 1


    r10 "Кто это у нас тут?"
    r10 "Эй, парни, смотрите кого я тут нашел"
    s10 "Да это же девчонка."#2
    a10 "Смотрите, у нее что-то в сумке!"#3
    s10 "Что-то блестит. Деньги что-ли?"#2
    r10 "А девочка-то при парокате…"
    r10 "Давно я такой себе хотел. Бугай, забери у нее сумку."

    h10 "Не трогайте меня!"

    a10 "Черт! Это не бабло, это склянки."#3
    s10 "“Не выбрасывай."#2
    r10 "Почему нет? На кой черт нам эти склянки."
    a10 "Может, у нее в карманах что-нибудь есть. Или же под юбкой.."#3

    h10_idea "Кажется, мне конец."

    s10 "Ух ты… Это не просто склянки. Это же.."#2
    r10 "Что там, Умник?"
    a10 "Говори, не томи! Ты единственный из нас умеешь читать."#3
    s10 "Вакцина, братаны. У нее восемь ампул с вакциной!"#2
    r10 "Да ладно!"
    a10 "Отлично! Хватит нам и еще останется!"#3
    a10 "Босс, что будем с ней делать?" #3
    r10 "Так, девка, мы забираем твою сумку с лекарствами. О нас никому не слова, понятно?"

    menu:
        "Забирайте сумку, только не трогайте меня!":
            r10 "Правильный выбор."
            r10 "Ведь некоторые события имеют тенденцию развиваться от плохого к худшему."
            h10_idea "О, боже… Я провалила это задание."
        "Верните лекарства, уроды!":
            r10 "Сама напросилась. Бугай, посмотри, что у нее в карманах."
            $ money_value = 0
            pause 1.0
            a10 "Забрал все, что было, Босс."

    $ Myo_value = 0

    pause

    jump chapter_11
