label chapter_7:
    $ location("«Серенити» 22")

    default was_in_ritual = False

    define h7 = CharacterAt(hero, (0.5 , 0.5), "left_tall")
    define h7_idea = CharacterAt(hero, (660, 180), "top_left_th")
    define d7 = CharacterAt(darklady, (0.6, 0.4), "right_tall", name = "мисс Д’Арк")
    define d7_long = CharacterAt(pharmacist, (1235, 140), "top_right_big", name = "мисс Д’Арк")

    scene black
    with dissolve

    show bg door lady:
        zoom 0.5
    with Dissolve(2)
    pause 1.0
    $ location("«Серенити» 22")

    show gg_ride:
        anchor (0.5, 0.8) zoom 0.2 xpos 1200-800 ypos 0.95
    with moveinleft

    pause 2.0

    show darklady:
        zoom 0.2
        xpos 1200 ypos 0.3
    with moveinright
    pause 1.0

    d7 "Ты вовремя… Я ждала тебя."

    h7 "Здравствуйте, мисс…"

    d7 "Называй меня Д’Арк."

    pause 1.0

    h7 "Вот Ваша посылка, возьмите, пожалуйста."

    d7 "Как тебя зовут, маленькая леди?"

    h7 "Эмм…"
    h7 "Мэллори, мисс Д’Арк. Меня зовут Мэллори."

    d7 "Мэллори…"
    d7 "Красивое имя."
    pause 1.0
    d7 "Мэллори, милое дитя, мне очень нужна твоя помощь. Без тебя я совершенно точно не справлюсь."
    d7 "Пойдём, пойдём скорее, Мэллори, время не ждёт."

    menu:
        "Извините, но нет. Мне нужно быстро доставить остальные заказы.":
            d7 "Ты меня очень разочаровала, но так уж и быть, вот деньги за заказ."
            $ Myo_value += 2
            $ money_value += 10
            pause 1.0
            hide darklady
            with dissolve
            pause 1.0
            $ map_info = ("7-8", "chapter_8")
            jump map
        "Если это не долго, то конечно, я готова помочь. Что от меня требуется?":
            d7 "Пойдем со мной в дом. Там в подвале надо кое-что передвинуть."
            h7 "Звучит странно, но я вам помогу."
            pause 1.0
            jump chapter_7s1


label chapter_7s1:

    $ was_in_ritual = True

    scene black
    with dissolve

    show bg basement:
        zoom 0.5
    with Dissolve(2)


    pause 1.0

    show gg_def:
        zoom 0.2
        xpos 20 ypos 400
    show darklady:
        zoom 0.2
        xpos 1200 ypos 0.3
    with dissolve

    h7_idea "Тут мрачная и давящая атмосфера... "
    h7 "Для чего здесь свечи, кубки и ножи? Красное — это чья-то кровь?!"
    d7 "Ничего не бойся, милая."
    d7 "Иди вот сюда, в центр пентаграммы, и ложись.  Руки будут связаны, но ты не переживай. Свечи уже горят, не хватает только тебя."
    d7 "Ну же, быстрее! Что ты медлишь? Время начинать!"
    menu:
        "Сбежать и взять мелочь.":
            h7 "Это очень ж-ж-жутко выглядит."
            h7 "Нет-нет, я к такому точно не готова."
            $ Myo_value += 1
            $ money_value += 5
            pause 1.0
            scene black
            with dissolve
            $ map_info = ("7-8", "chapter_8")
            jump map
            
        "Выглядит жутковато, но попробовать можно.":
            h7 "Звучит странно, но я готова."
            h7 "А зачем синяя изолнета?…"
            d7 "Давай сюда руки, узнаешь."
            h7 "Мне немного туго, я не могу пошевелиться."
            h7 "Что будет дальше? Мне страшно!"

            d7 "Кляп. Дальше будет кляп."
            pause 1.0
            h7 "Ммжлмл... (нечленораздельная речь)"

            h7_idea "Я думала, что этот день не может быть еще хуже. Но, кажется, ошиблась."
            h7_idea "Скорее всего, это будет стоить мне жизни."

            d7 "Уииииии! Тёмные Силы, Бессмертные Духи, явитесь на мой зов!!!"
            d7_long "Пусть свершится предначертанное, умрёт больной и невинный, души голода с окраин получат свои жертвы, да омоется это в слезах ребёнка."
            d7 "Да придёт в город Дух Мёрфи, да будут его законы."
            d7 "Оууу!"
            pause 2.0 #sound
            d7 "Вот всё и кончилось. Сейчас я развяжу тебя и сниму кляп."

            h7 "Фух…"
            h7 "Эм… Можно, я пойду?"
            d7 "Да, вот тебе деньги."
            $ Myo_value += 0
            $ money_value +=10
            h7 "Это было лучше, чем я думала."
            d7 "И ещё, Мэллори, на будущее."
            d7 "Не ходи с незнакомыми людьми в подвал."
            d7 "Ну это я так, к слову."

            $ map_info = ("7-8", "chapter_8")
            jump map
