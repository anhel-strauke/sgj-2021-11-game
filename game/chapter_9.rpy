label chapter_9:
    $ location("Набережная Бьеншан 8")

    define h9 = CharacterAt(hero, (584 , 440), "left_very_tall")
    define h9_long = CharacterAt(hero, (430 , 740), "top_left_big")
    define h9_idea = CharacterAt(hero, (740, 140), "top_left_th")
    define vv = CharacterAt(vivi, (1275, 400), "right_very_tall")
    define n = CharacterAt(nelly, (1235, 540), "right_very_tall")
    define vv_long = CharacterAt(vivi, (1235, 140), "top_right_big")

    scene black
    with dissolve

    show bg doll:
        zoom 0.5
    show vv:
        zoom 0.2
        xpos 1200 ypos 0.3
    with Dissolve(2)

    pause 1.0

    $ location("Набережная Бьеншан 8")

    if not doll_broken:
        jump chapter_9_doll_ok

    vv "Не волнуйся, солнышко. Курьер уже в пути."
    vv "Скоро ты увидишь свою куклу."

    n "Мама, я устала ждать курьера. Где же он?"

    pause 1.0

    vv "Вот он! Ты видишь, Нелли?"

    n "Но мама, это же девочка!"

    vv "Ну да. Девочки тоже могут быть курьерами, солнышко."

    n "Мама, я хочу стать курьером, когда вырасту!"

    vv "Ха-ха. Почему, солнышко?"

    n "Курьеры приносят людям то, что они очень хотят!"
    n "Я тоже так хочу."

    vv "Хорошо. Но сначала тебе нужно выздороветь."
    vv "Как раз этот курьер должен принести тебе специальную куклу, в которой есть лекарства из той аптеки."

    n "Я помню госпожу из аптеки. Она такая страшная…"

    vv "Нелли, что за манеры! Нехорошо так говорить о других взрослых."

    pause 1.0

    show gg_def:
        zoom 0.2
        xpos 60 ypos 360
    with moveinleft

    h9 "Добрый день!"
    h9 "У меня посылка для маленькой Нелли."

    vv "Мы вас давно ждём! Что заставило вас задержаться?"

    h9 "Я попала в…"

    n "Моя кукла! Где моя кукла?!"

    vv "Нелли, прояви терпение! Что за несносный ребенок…"
    vv "И всё же мы рады, что вы до нас добрались."
    vv "А теперь, пожалуйста, вручите нам нашу посылку."

    show doll_1:
        zoom 0.25
        xpos 365 ypos 495
        rotate -30
    with Dissolve(1)

    pause 1.0

    vv "Боже, что случилось с куклой?"

    n "Мама! Она вся порванная!"

    h9 "Мне очень жаль. Когда я ехала к вам, то попала в аварию."
    h9 "Сейчас очень сильное движение на улице, ещё и задымление…"

    vv "Простите, как вас зовут?"

    h9 "Мэллори."

    vv "Понимаете, Мэллори, у моей дочери лихорадка."
    vv  "Госпожа Амелинда уверила меня, что запах трав и специального порошка в этой кукле помогут сбить её температуру…"

    h9 "Мне, правда, очень жаль. Но вы ещё можете зашить эту куклу."

    n "Мамочка, у нее из животика всё вылетело!"

    vv "Я не уверена, что теперь в этом есть смысл…"
    vv "Нелли так ждала эту куклу."

    menu:
        "Так или иначе, кукла у вас. Вы должны заплатить мне за доставку!":
            vv "Наглая девчонка! Тебе самой-то не стыдно? Вместо куклы ты принесла больному ребенку кусок рваного тряпья!"
            h9 "Я попала в аварию, пока ехала к вам! И, несмотря на это, доставила куклу в срок!"
            pause 1.0
            vv "…На чаевые не надейся."
            $ Myo_value += 1
            $ money_value += 5
        "Оставьте деньги за доставку себе как компенсацию ущерба.":
            vv "Мэллори…"
            vv "Ты серьезно? Ведь тебе нелегко пришлось, как ты сама говоришь."
            h9 "Я найду ещё способ заработать. Главное, снова не попасть в аварию."
            h9 "Ведь из всех возможных неприятностей…"
            vv "…произойдёт именно та, ущерб от которой больше."
            vv "Так говорил мой муж. Береги себя, Мэллори."

    hide doll_1
    with dissolve

    pause 1.0

    hide gg_def
    with moveoutleft

    vv "Не плачь, Нелли. Я накоплю денег и куплю тебе новую куклу."
    n "Мамочка.."
    vv "Что, солнышко?"
    n "Я больше не хочу быть курьером."

    pause 1.0

    $ map_info = ("9-10", "chapter_10")
    jump map

label chapter_9_doll_ok:

    show gg_def:
        zoom 0.2
        xpos 60 ypos 360
    with moveinleft

    h9 "Добрый день!"
    h9 "У меня посылка для маленькой Нелли."

    vv "Мы вас давно ждём!"

    h9 "Летела на максимальном пару."

    n "Моя кукла! Где моя кукла?!"

    vv "Нелли, прояви терпение! Что за несносный ребёнок.…"
    vv "И всё же мы рады, что вы до нас добрались."
    vv "А теперь, пожалуйста, вручите нам нашу посылку."

    show doll_2:
        alpha 0
        zoom 0.25
        xpos 485 ypos 375
        linear 1 alpha 1
        parallel:
            ease 1.0 xpos 1320
        parallel:
            ease 1.0 ypos 545
    pause 2.0
    show doll_2:
        alpha 1 zoom 0.25 xpos 1320 ypos 545

    n "Какая красивая…"
    n "Мама! От куклы плохо пахнет!"

    h9 "Так и должно быть, Нелли. Внутри этой куклы целебные травы и волшебный порошок."

    n "Волшебный?"

    h9 "Да. Этот порошок поможет тебе выздороветь. Его передала тебе госпожа Амелинда."

    n "Мама, а эта кукла что, волшебная, получается?"

    vv "Скорее лечебная, милая."

    pause 1.0

    vv "Спасибо вам за доставку. Возьмите эти деньги. Вы их заработали."

    h9 "Я делаю это ради котика."

    vv "Уверена, вы заботливая хозяйка."

    $ Myo_value += 2
    $ money_value += 10

    pause 1.0

    h9 "Спасибо вам. Но мне пора бежать."
    h9 "Скорейшего выздоровления, малышка Нелли. Всего хорошего!"

    n "До свидания, тётя курьер!"

    vv "Будьте внимательны на дорогах."

    hide gg_def
    with moveoutleft

    pause 1.0

    $ map_info = ("9-10", "chapter_10")

    jump map
